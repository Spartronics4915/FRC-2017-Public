import org.gradle.api.java.archives.internal.DefaultManifest
import java.text.SimpleDateFormat

plugins {
    id "java"
    id "eclipse"
    id "edu.wpi.first.GradleRIO" version "2019.0.0-alpha-3"
}

def ROBOT_CLASS = "com.spartronics4915.frc2019.Robot"

deploy {
    targets {
        target("roborio", edu.wpi.first.gradlerio.frc.RoboRIO) {
            // team number is loaded either from .wpilib/wpilib_preferences.json
            // or from the command line.
            team = getTeamNumber()
        }
    }
    artifacts {
        artifact('frcJava', edu.wpi.first.gradlerio.frc.FRCJavaArtifact) {
            targets << "roborio"
            debug = getDebugOrDefault(false)
        }
    }
}

repositories {
    mavenCentral() // Needed for json-simple, and jama
}

dependencies {
    compile group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1'
    compile group: 'gov.nist.math', name: 'jama', version: '1.0.3' // Linear algebra
    compile wpilib()
    compile ctre()
    compile navx()
}

jar {
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    manifest edu.wpi.first.gradlerio.GradleRIOPlugin.javaManifest(ROBOT_CLASS)
    manifest { DefaultManifest mf ->
        String user = System.getProperty("user.name");
        SimpleDateFormat fmt = new SimpleDateFormat("MMMMM dd, hh:mm a");
        ByteArrayOutputStream gitinfo = new ByteArrayOutputStream();
        exec {
            ignoreExitValue true
            commandLine 'git', 'describe', "--tags", "--dirty"
            standardOutput = gitinfo
        }
        mf.attributes "Built-By": user,
                      "Built-At": fmt.format(new Date()),
                      "Code-Version": gitinfo.toString().trim()
    }
}

// Force Java 8 Compatibility mode for deployed code, in case the develoment
// system is using Java 10.
compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

wrapper {
    gradleVersion = '4.9'
}
